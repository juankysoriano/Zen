configurations {
    codequality
}

repositories {
    mavenCentral()
}

dependencies {
    codequality 'net.sourceforge.pmd:pmd:5.1.2'
}

task pmd(type: AndroidPmdTask) {
    ignoreFailures true
    showViolations false
}

check.dependsOn(pmd)

///////////////////////////////////////////////
////////////// Groovy Task Class //////////////
///////////////////////////////////////////////
import org.gradle.api.internal.project.IsolatedAntBuilder

/**
 * See parameters at http://pmd.sourceforge.net/pmd-4.2.6/ant-task.html
 */
class AndroidPmdTask extends DefaultTask {
    @InputFile
    @Optional
    File rulesetFile = new File("$project.rootDir/analysis-tools/pmd/ruleset.xml")
    @InputFile
    @Optional
    File xslFile = new File("$project.rootDir/analysis-tools/pmd/pmd-nicerhtml.xsl")
    @OutputFile
    @Optional
    File outputFile = new File("$project.buildDir/reports/pmd/pmd-${project.name}.xml")
    def outputHtmlFile = outputFile.absolutePath.replaceFirst(~/\.[^\.]+$/, ".html")
    FileCollection pmdClasspath = project.configurations.codequality
    Boolean ignoreFailures = false
    Boolean showViolations = true
    Project gradleProject = project

    def AndroidPmdTask() {
        description = 'Runs PMD against Android sourcesets.'
        group = 'Code Quality'
    }

    @TaskAction
    def runPmd() {
        outputFile.parentFile.mkdirs()
        def antBuilder = services.get(IsolatedAntBuilder)
        antBuilder.withClasspath(pmdClasspath).execute {
            ant.taskdef(name: 'pmd', classname: 'net.sourceforge.pmd.ant.PMDTask')
            ant.pmd(shortFilenames: 'true',
                    failonruleviolation: !ignoreFailures,
                    rulesetfiles: rulesetFile.toURI().toString()) {
                formatter(type: 'xml', toFile: outputFile, toConsole: showViolations)
                gradleProject.each { submodule ->
                    if (!submodule.plugins.hasPlugin("com.android.application") && !submodule.plugins.hasPlugin("com.android.library")) {
                        return;
                    }

                    submodule.android.sourceSets.each { sourceSet ->
                        sourceSet.java.each { file ->
                            file.getSrcDirs().each {
                                if (it.exists() && !it.path.contains("test")) {
                                    fileset(dir: it)
                                }
                            }
                        }
                    }
                }
            }
            ant.xslt(in: outputFile,
                    style: xslFile,
                    out: outputHtmlFile)
        }
        gradleProject.REPORT_LOCATIONS += "PMD wrote HTML report to " + "file://${outputHtmlFile} \n"
    }
}
